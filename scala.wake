
# Important directories
def root = mkdir 0775 ".scala-wake"
def bloopDir = mkdir 0775 ".bloop"
def bloopInstall = mkdir 0775 "bin"
global def ivyCache = mkdir 0775 "{root.getPathName}/cache"

#global def coursierBin _ =
#  def version = "1.1.0-M11-2"
#  def dir = mkdir 0775 "{root.getPathName}/bin"
#  def getter = sources here 'getCoursier\.sh' | head
#  def cmd = getter.getPathName, dir.getPathName, version, Nil
#  def doit = job cmd (dir, getter, Nil)
#  doit.getJobOutput

# Should be installed by wit
global def coursierBin =
  #def bins = files "{root.getPathName}/bin" ".*coursier.*"
  # FIXME Using the one from manually downloaded bloop
  def bins = files bloopInstall.getPathName ".*coursier.*"
  def bin = bins | head | makeStatePath
  try (\_ raise "coursier not found!") bin

global def millBin _ =
  def version = "0.3.5"
  def dir = mkdir 0775 "{root.getPathName}/bin"
  def getter = sources here 'getMill\.sh' | head
  def cmd = getter.getPathName, dir.getPathName, version, Nil
  def doit = job cmd (getter, dir, Nil)
  doit.getJobOutput


# TODO Do this right
# This should be installed by wit
global def bloopBin = makeStatePath "{bloopInstall.getPathName}/bloop"

# TODO kind of ugly
global def bloopExe _ =
  def launcher = sources here "bloop_server" | head
  def timeout = sources here "bloop_timeout" | head
  def common = sources here "common.py" | head
  def pidfile = files here '.*\.pid' | head | makeStatePath
  def other = files bloopInstall.getPathName '.*'
  def cmd = launcher.getPathName, Nil
  #def visible = pidfile, launcher, timeout, common, (map makeStatePath other)
  def visible = launcher, timeout, common, (map makeStatePath other)
  def doit =
    makeManualPlan cmd visible (\_ Nil)
    | setPlanKeep False # Always rerun to make sure we have a running bloop server
    | runJob
  if doit.getJobStatus == 0 then bloopBin else raise "Error! Unable to launch bloop!"


# TODO
# - Rename?
# - Re-enable `-m offline`
# Given List IvyDep, returns List String
# Return is paths to jars of the input IvyDeps and their transitive dependencies
global def coursierFetch deps =
  def depStrs = map ivyDepToCoursier deps
  def cachedDeps = files ivyCache.getPathName '.*' | map makeStatePath
  #def cmd = coursierBin.getPathName, "fetch", "-q", "--cache", ivyCache.getPathName, "-m", "offline", depStrs
  def cmd = coursierBin.getPathName, "fetch", "-q", "--cache", ivyCache.getPathName, depStrs
  def doit = job cmd (coursierBin, cachedDeps)
  if doit.getJobStatus == 0 then
    doit.getJobStdout | tokenize '\n' | filter ("{_}x" !=~ "x") # filter non-empty
  else raise "Cannot resolve ivy dependencies, have you run `wit update` (currently unimplemented)?"

# TODO Dotty support?
tuple ScalaVersion =
  global Major Integer
  global Minor Integer

def scalaVersionToString = match _
  ScalaVersion major minor = "2.{str(major)}.{str(minor)}"

global def makeScalaVersion str =
  def err = raise "Only Scala 2.X.Y accepted, got '{str}'"
  match (tokenize '\.' str)
    majormajor, major, minor, Nil =
      if majormajor ==~ "2" then ScalaVersion (int major) (int minor) else err
    _ = err

# TODO additional resolvers/repositories
tuple IvyDep =
  global Org String
  global Name String
  global Rev String

global def makeScalaIvyDep scalaVersion string =
  def err = raise "Malformed Scala Ivy Dep '{string}'"
  match (tokenize ':' string)
    org, name, rev, Nil = IvyDep org name rev
    org, blank, name, rev, Nil =
      def namex = "{name}_2.{str(scalaVersion.getScalaVersionMajor)}"
      if "{blank}x" ==~ "x" then IvyDep org namex rev else err
    _ = err

global def makeJavaIvyDep string =
  match (tokenize ':' string)
    org, name, rev, Nil = IvyDep org name rev
    _ = raise "Malformed Java Ivy Dep '{string}'"

def ivyDepToCoursier = match _
  IvyDep org name rev = "{org}:{name}:{rev}"

global def scalaCompilerIvyDep version =
  IvyDep "org.scala-lang" "scala-compiler" (scalaVersionToString version)
global def scalaLibraryIvyDep version =
  IvyDep "org.scala-lang" "scala-library" (scalaVersionToString version)

tuple ScalaModule =
  global Name         String
  global RootDir      String
  global ScalaVersion ScalaVersion
  global IvyDeps      List IvyDep
  global Deps         List ScalaModule
  SourceDirs_         List String
  ResourceDirs        List String

global def getScalaModuleSourceDirs = match _
  ScalaModule _ dir _ _ _ sourceDirs _ =
    map ("{dir}/{_}") sourceDirs

# Should the source dirs be relative to root or incldue it?
global def makeSBTScalaModule name dir version =
  def scalaVersion = makeScalaVersion version
  def sourceDirs = "src/main/scala", "src/main/java", Nil | map ("{dir}/{_}")
  def resourceDirs = "{dir}/src/main/resources", Nil
  ScalaModule name dir scalaVersion Nil Nil sourceDirs resourceDirs

# TODO implement scalacOptions
global def bloopScalaLibJSON scalaVersion =
  def ivyDep = scalaCompilerIvyDep scalaVersion
  def org = Pair "organization" (JString ivyDep.getIvyDepOrg)
  def name = Pair "name" (JString ivyDep.getIvyDepName)
  def version = Pair "version" (JString ivyDep.getIvyDepRev)
  def options = Pair "options" (JArray Nil)
  def jars = Pair "jars" $ coursierFetch (ivyDep, Nil) | map JString | JArray
  def setup =
    def fields =
      Pair "order" (JString "mixed")
      , Pair "addLibraryToBootClasspath" (JBoolean True)
      , Pair "addCompilerToClasspath" (JBoolean False)
      , Pair "addExtraJarsToClasspath" (JBoolean False)
      , Pair "manageBootClasspath" (JBoolean True)
      , Pair "filterLibraryFromClasspath" (JBoolean True)
      , Nil
    Pair "setup" (JObject fields)
  JObject (org, name, version, options, jars, setup, Nil)

#def buildDir module = "{root.getPathName}/{module.getScalaModuleName}"
def bloopOutDir module = "{bloopDir.getPathName}/{module.getScalaModuleName}"
def bloopClassesDir module = "{root.getPathName}/{module.getScalaModuleName}/classes"

# TODO
# - implement module dependencies (see jdeps)
global def bloopScalaModuleJSON scalaModule =
  def bloopVersion = "1.2.5"
  def project = match scalaModule
    ScalaModule name dir version ivyDeps modDeps sourceDirs resourceDirs =
      def myDir = bloopOutDir scalaModule
      def scalaLib = scalaLibraryIvyDep version
      def ivyClasspath = (scalaLib, ivyDeps) | coursierFetch
      def depClasspath = modDeps | map bloopClassesDir
      def fields =
        Pair   "name" (JString name)
        , Pair "directory" (JString dir)
        , Pair "sources" (JArray (map (JString "{dir}/{_}") sourceDirs))
        , Pair "dependencies" (JArray (map getScalaModuleName modDeps | map JString))
        , Pair "classpath" (ivyClasspath ++ depClasspath | map JString | JArray)
        , Pair "out" (bloopOutDir scalaModule | JString)
        , Pair "classesDir" (bloopClassesDir scalaModule | JString)
        , Pair "scala" (bloopScalaLibJSON version)
        , Nil
      #JObject (jname, jdir, jsources, jdeps, jcp, jout, jclasses, jscala, Nil)
      JObject fields
  def bloop = Pair "version" (JString bloopVersion)
  def proj = Pair "project" project
  JObject (bloop, proj, Nil)

global def bloopConfig module = memoize 0 (
  def content = bloopScalaModuleJSON module | prettyJSON
  write 0664 (bloopConfigName module) content
)

global def bloopConfigName module =
  def name = module.getScalaModuleName
  "{bloopDir.getPathName}/{name}.json"

# TODO
# - Include ivy and transitive ivy deps in visible files
# - Include Bloop configs and build dirs for dependencies
# - Use local bloop
# - Should the bloop invocation be a regular plan or a manual plan?
#   - Value in regular plan is it can tell me if I'm missing input files
#   - We have to set inputs and outputs anyway
global def compileScalaModule  module =
  def config = module.bloopConfig
  def outdir = mkdir 0775 module.bloopClassesDir
  def statedir = mkdir 0775 module.bloopOutDir
  def bin = bloopExe Unit
  def modDeps = module.getScalaModuleDeps
  def allInputs =
    def sourceFiles =
      def dirs = module.getScalaModuleSourceDirs
      map (sources _ '.*\.(scala|java)') dirs | flatten
    def depClassFiles =
      map compileScalaModule modDeps | flatten
    sourceFiles ++ depClassFiles
  def depConfigFiles =
    map bloopConfig modDeps
  def cmd = bin.getPathName, "compile", module.getScalaModuleName, Nil
  def visible = (bin, config, outdir, statedir, depConfigFiles)
  def plan =
    makePlan cmd visible
    | setPlanFnOutputs (\_ files outdir.getPathName '.*\.class')
    | setPlanFnInputs (\_ allInputs | map getPathName)
  plan.runJob.getJobOutputs

global def jack _ = match foo
  ScalaModule name dir version ivyDeps modDeps sourceDirs resourceDirs =
    def scalaLib = scalaLibraryIvyDep version
    #Pair "classpath" $ (scalaLib, ivyDeps) | coursierFetch | map JString | JArray
    (scalaLib, ivyDeps) | coursierFetch
